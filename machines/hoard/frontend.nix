{
  config,
  ...
}:
let
  inherit ((import ./ports.nix { inherit config; })) ports;
in
{
  services.cloudflared = {
    enable = true;
    certificateFile = config.sops.secrets.cloudflare-cert.path;
    # cloudflared tunnel create hoard-tunnel
    tunnels.hoard-tunnel = {
      default = "http_status:404";
      credentialsFile = config.sops.secrets.cloudflare-tunnel.path;
      ingress = {
        # cloudflared tunnel route dns hoard-tunnel <hostname>
        "overseerr.${config.networking.domain}" = "http://localhost:${toString ports.overseerr}";
        "tautulli.${config.networking.domain}" = "http://localhost:${toString ports.tautulli}";
      };
    };
  };

  my.webGateway = {
    enable = true;
    services = {
      home.port = ports.home;
      glances.port = ports.glances;
      maintainerr.port = ports.maintainerr;
      qbittorrent.port = ports.qbittorrent;
      sabnzbd = {
        port = ports.sabnzbd;
        apiBypassPrefixes = [
          "/api"
          "/sabnzbd/api"
        ];
      };
      sonarr = {
        port = ports.sonarr;
        apiBypassPrefixes = [
          "/api"
          "/feed"
        ];
      };
      radarr = {
        port = ports.radarr;
        apiBypassPrefixes = [
          "/api"
          "/feed"
        ];
      };
      lidarr = {
        port = ports.lidarr;
        apiBypassPrefixes = [
          "/api"
          "/feed"
        ];
      };
      prowlarr = {
        port = ports.prowlarr;
        apiBypassPrefixes = [ "/api" ];
      };
      bazarr = {
        port = ports.bazarr;
        apiBypassPrefixes = [ "/api" ];
      };

      # multi-user identity lives in these apps (Plex accounts, Jellyfin
      # accounts, Tautulli's own login), and overseerr/tautulli are also
      # reachable from the internet through the Cloudflare tunnel above
      overseerr = {
        port = ports.overseerr;
        auth = "native";
      };
      tautulli = {
        port = ports.tautulli;
        auth = "native";
      };
      jellyfin = {
        port = ports.jellyfin;
        auth = "native";
      };
    };
    loopbackGate.clients = [
      config.services.sonarr.user
      config.services.radarr.user
      config.services.lidarr.user
      config.services.bazarr.user
      config.services.recyclarr.user
      config.users.users.maintainerr.name
    ];
    sso = {
      enable = false;
      sopsFile = ../../secrets/hoard/authelia.yaml;
      users.${config.my.user} = {
        displayName = "Glib";
        email = "me@${config.my.domain}";
      };
    };
  };

  sops.secrets = {
    cloudflare-tunnel = {
      sopsFile = ../../secrets/hoard/cloudflare-tunnel.json;
      mode = "0440";
      format = "json";
      key = ""; # we want the entire file
    };
    cloudflare-cert = {
      sopsFile = ../../secrets/common/cloudflare-cert.pem;
      mode = "0440";
      format = "binary";
    };
  };
}
